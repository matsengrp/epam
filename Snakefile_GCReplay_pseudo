import json
import subprocess

dms_data_file = "data/gcreplay/final_variant_scores.csv"
shm_hc_data_file = 'data/gcreplay/chigy_hc_mutation_rates_nt.csv'
shm_lc_data_file = 'data/gcreplay/chigy_lc_mutation_rates_nt.csv'
GCREPLAY_IGH_MODELS = [
    #("GCReplayOptSHM_igh",           "GCReplayOptSHM", { "shm_data_file": shm_hc_data_file}),
    ("GCReplayOptSHMDMSSigmoid_igh", "GCReplayOptSHMDMSSigmoid", { "shm_data_file": shm_hc_data_file, "dms_data_file": dms_data_file, "chain": "heavy"}),
]
GCREPLAY_IGK_MODELS = [
    #("GCReplayOptSHM_igk",           "GCReplayOptSHM", { "shm_data_file": shm_lc_data_file}),
    ("GCReplayOptSHMDMSSigmoid_igk", "GCReplayOptSHMDMSSigmoid", { "shm_data_file": shm_lc_data_file, "dms_data_file": dms_data_file, "chain": "light"}),
]

igh_model_name_to_spec = {
    model_name: [model_class, json.dumps({**model_params, "model_name": model_name})]
    for model_name, model_class, model_params in GCREPLAY_IGH_MODELS
}
igk_model_name_to_spec = {
    model_name: [model_class, json.dumps({**model_params, "model_name": model_name})]
    for model_name, model_class, model_params in GCREPLAY_IGK_MODELS
}

igh_pcp_inputs = glob_wildcards("pcp_gcreplay_pseudo_inputs/igh/{name}.csv").name
igk_pcp_inputs = glob_wildcards("pcp_gcreplay_pseudo_inputs/igk/{name}.csv").name


rule all:
    input:
        "output_pseudo/igh/combined_performance.csv",
        "output_pseudo/igk/combined_performance.csv",


rule run_igh_model:
    input:
        in_csv="pcp_gcreplay_pseudo_inputs/igh/{pcp_input}.csv",
    output:
        aaprob="output_pseudo/igh/{pcp_input}/{model_name}/aaprob.hdf5",
        performance="output_pseudo/igh/{pcp_input}/{model_name}/performance.csv",
    params:
        model_class=lambda wildcards: igh_model_name_to_spec[wildcards.model_name][0],
        model_params=lambda wildcards: igh_model_name_to_spec[wildcards.model_name][1],
    benchmark:
        "output_pseudo/igh/{pcp_input}/{model_name}/timing.tsv"
    shell:
        """
        mkdir -p output_pseudo/igh/{wildcards.pcp_input}/{wildcards.model_name}
        epam aaprob {params.model_class} '{params.model_params}' {input.in_csv} {output.aaprob}
        epam evaluate {output.aaprob} {output.performance}
        """


rule run_igk_model:
    input:
        in_csv="pcp_gcreplay_pseudo_inputs/igk/{pcp_input}.csv",
    output:
        aaprob="output_pseudo/igk/{pcp_input}/{model_name}/aaprob.hdf5",
        performance="output_pseudo/igk/{pcp_input}/{model_name}/performance.csv",
    params:
        model_class=lambda wildcards: igk_model_name_to_spec[wildcards.model_name][0],
        model_params=lambda wildcards: igk_model_name_to_spec[wildcards.model_name][1],
    benchmark:
        "output_pseudo/igk/{pcp_input}/{model_name}/timing.tsv"
    shell:
        """
        mkdir -p output_pseudo/igk/{wildcards.pcp_input}/{wildcards.model_name}
        epam aaprob {params.model_class} '{params.model_params}' {input.in_csv} {output.aaprob}
        epam evaluate {output.aaprob} {output.performance}
        """
        

rule combine_igh_performance_files:
    input:
        expand(
            "output_pseudo/igh/{pcp_input}/{model_name}/performance.csv",
            pcp_input=igh_pcp_inputs,
            model_name=igh_model_name_to_spec.keys(),
        ),
    output:
        "output_pseudo/igh/combined_performance.csv",
        "output_pseudo/igh/combined_timing.csv",
    run:
        input_files = ",".join(input)
        input_timing_files = ",".join(
            f"output_pseudo/igh/{pcp_input}/{model_name}/timing.tsv"
            for pcp_input in igh_pcp_inputs
            for model_name in igh_model_name_to_spec.keys()
        )
        output_file = output[0]
        output_timing_file = output[1]
        subprocess.run(
            f"epam concatenate_csvs {input_files} {output_file}", shell=True, check=True
        )
        subprocess.run(
            f"epam concatenate_csvs {input_timing_files} {output_timing_file} --is_tsv --record_path",
            shell=True,
            check=True,
        )

rule combine_igk_performance_files:
    input:
        expand(
            "output_pseudo/igk/{pcp_input}/{model_name}/performance.csv",
            pcp_input=igk_pcp_inputs,
            model_name=igk_model_name_to_spec.keys(),
        ),
    output:
        "output_pseudo/igk/combined_performance.csv",
        "output_pseudo/igk/combined_timing.csv",
    run:
        input_files = ",".join(input)
        input_timing_files = ",".join(
            f"output_pseudo/igk/{pcp_input}/{model_name}/timing.tsv"
            for pcp_input in igk_pcp_inputs
            for model_name in igk_model_name_to_spec.keys()
        )
        output_file = output[0]
        output_timing_file = output[1]
        subprocess.run(
            f"epam concatenate_csvs {input_files} {output_file}", shell=True, check=True
        )
        subprocess.run(
            f"epam concatenate_csvs {input_timing_files} {output_timing_file} --is_tsv --record_path",
            shell=True,
            check=True,
        )
